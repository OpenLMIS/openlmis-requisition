buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE"
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.0"
    id "org.sonarqube" version "2.2.1"
    id "com.moowork.node" version "0.12"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'pmd'

group = serviceGroup
version = serviceVersion
archivesBaseName = rootProject.name
project.ext.buildTime = java.time.Instant.now().toString() // for versioning
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/" }
    maven { url "http://jasperreports.sourceforge.net/maven2/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.projectlombok:lombok:1.16.8"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.postgresql:postgresql:9.4.1208"
    compile "org.springframework:spring-test"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-hibernate4"
    compile "net.sf.supercsv:super-csv-dozer:2.4.0"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.security.oauth:spring-security-oauth2"
    compile "net.sf.jasperreports:jasperreports:6.3.0"
    compile "com.lowagie:itext:2.1.7"
    compile "org.olap4j:olap4j:1.2.0"
    compile 'net.sf.jasperreports:jasperreports:6.1.1'
    compile 'commons-io:commons-io:2.4'
    compile 'org.springframework:spring-context-support'
    compile 'commons-jxpath:commons-jxpath:1.3'
    compile 'org.webjars:swagger-ui:2.2.6'
    compile 'org.openlmis:openlmis-service-util:3.0.0-SNAPSHOT'
    compile 'org.joda:joda-money:0.12'
    compile 'org.jadira.usertype:usertype.core:5.0.0.GA'
    compile 'org.apache.poi:poi:3.10.1'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.javers:javers-spring-boot-starter-sql:2.8.1'
    compile 'org.flywaydb:flyway-core:4.0.3'

    testCompile 'io.rest-assured:rest-assured:3.0.0'
    testCompile 'io.rest-assured:json-schema-validator:3.0.0'
    testCompile 'io.rest-assured:spring-mock-mvc:3.0.0'
    testCompile "com.jayway.restassured:rest-assured:2.7.0"

    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "guru.nidi.raml:raml-tester:0.8.9"
    testCompile "junit:junit"
    testCompile "org.assertj:assertj-core:3.6.1"
    testCompile "org.mockito:mockito-core:1.+"
    testCompile "org.powermock:powermock-api-mockito:1.6.5"
    testCompile "org.javassist:javassist:3.20.0-GA"
    testCompile "org.powermock:powermock-module-junit4:1.6.5"
    testCompile "com.github.tomakehurst:wiremock:1.58"
}

idea {
    project {
        vcs = 'Git'
        ipr.withXml { xmlFile ->
            // enable 'Annotation Processors', source: https://gist.github.com/stephanos/8645809
            xmlFile.asNode().component
                    .find { it.@name == 'CompilerConfiguration' }['annotationProcessing'][0]
                    .replaceNode {
                annotationProcessing {
                    profile(default: true, name: 'Default', useClasspath: 'true', enabled: true)
                }
            }
        }
    }
}

flyway {
    url = "$System.env.DATABASE_URL"
    user = "$System.env.POSTGRES_USER"
    password = "$System.env.POSTGRES_PASSWORD"
    schemas = ['requisition']
    sqlMigrationPrefix = ''
    placeholderPrefix = '#['
    placeholderSuffix = ']'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
    mustRunAfter test
    environment 'BASE_URL', "http://localhost"
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
    reports.junitXml.destination = file("${testResultsDir}/${name}")
}

// Usage: gradle generateMigration [-PmigrationName=name_of_migration]
//          Defaults to 'migration' as migration name
// Example: gradle generateMigration -PmigrationName=add_column_to_users
//          Will create a file in migration folder with name yyyyMMddHHmmssSSS_add_column_to_users.sql.
task generateMigration << {
    description 'Creates an empty new file within the src/main/resources/db/migration directory into which developers can add new SQL migration code.'
    def fileName = project.hasProperty('migrationName') ? migrationName : 'migration'
    def timestamp = new Date().format('yyyyMMddHHmmssSSS', TimeZone.getTimeZone('GMT'))
    def fullFileName = "${timestamp}__${fileName}.sql"
    def migrationFile = new File(sourceSets.main.resources.srcDirs.first(),'db/migration/'+fullFileName)
    migrationFile.createNewFile()
}

task checkApiIsRaml(type:Exec) {
    executable "raml-cop"
    args "src/main/resources/api-definition.yaml"
}

task demoDataSeed(type:Exec) {
    executable "./demo-data/demo_seed.sh"
    args "./demo-data", "node ./demo-data/seed_generator.js"
}

configure(checkApiIsRaml) {
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = 'Verify that the api-specification is valid RAML'
}

jacocoTestReport {
    group = "reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }

    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}

checkstyle {
    toolVersion = "6.19"
}

//Usage: gradle sonarqube
sonarqube {
    properties {
        property "sonar.projectName", "OpenLMIS Requisition"
        property "sonar.projectKey", "org.sonarqube:openlmis-requisition"
        property "sonar.host.url", "http://sonar.openlmis.org"
        property "sonar.projectVersion", version
        property "sonar.java.coveragePlugin", "jacoco"
        //Tells SonarQube where the unit tests execution reports are
        property "sonar.junit.reportsPath", "build/test-results/test"
        //Tells SonarQube where the unit tests code coverage report is
        property "sonar.jacoco.reportPath", "build/jacoco/test.exec"
        //Tells SonarQube where the integration tests code coverage report is
        property "sonar.jacoco.itReportPath", "build/jacoco/integrationTest.exec"
	properties["sonar.tests"] += sourceSets.integrationTest.java
    }
}

pmd {
    toolVersion = '5.4.0'
    consoleOutput= true
    ignoreFailures = false
    ruleSetFiles = files("config/pmd/ruleset.xml")
    reportsDir = file("build/reports/pmd")
}

tasks.withType(Pmd){
    reports {
        xml.enabled true
        html.enabled true
    }
}

test {
    testLogging {
        events 'started', 'passed'
        exceptionFormat = 'full'
    }
}

apply from: "documentation.gradle"
integrationTest {
    dependsOn ramlToSwagger
}

processResources {
    // we want the generated swagger spec file included in the output jar
    finalizedBy ramlToSwagger
    // update version information in build
    filesMatching('**/version.properties') {
        expand(project.properties)
    }
}

check {
    dependsOn checkApiIsRaml
    dependsOn integrationTest
}

apply from: "registration.gradle"

build {
    dependsOn jacocoTestReport
    dependsOn check
    dependsOn ramlToHtml
    dependsOn copyRamlHtmlToBuild
    dependsOn demoDataSeed
    dependsOn copyConsulRegistrationToBuild
}
